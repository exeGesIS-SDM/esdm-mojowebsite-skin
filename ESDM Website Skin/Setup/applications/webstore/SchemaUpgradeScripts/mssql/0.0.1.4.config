ALTER TABLE [dbo].ws_Offer ADD
	Url nvarchar(255) NULL 
GO

ALTER TABLE [dbo].ws_OfferHistory ADD
	Url nvarchar(255) NULL 
GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Offer_Insert]

/*
Author:   			Joe Audette
Created: 			2007-02-28
Last Modified: 		2008-07-16
*/

@Guid uniqueidentifier,
@StoreGuid uniqueidentifier,
@IsVisible bit,
@IsSpecial bit,
@TaxClassGuid uniqueidentifier,
@Url nvarchar(255),
@Created datetime,
@CreatedBy uniqueidentifier,
@CreatedFromIP nvarchar(255),
@IsDeleted bit,
@LastModified datetime,
@LastModifiedBy uniqueidentifier,
@LastModifiedFromIP nvarchar(255)

	
AS
INSERT INTO 	[dbo].[ws_Offer] 
(
				[Guid],
				[StoreGuid],
				[IsVisible],
				[IsSpecial],
				[Created],
				[CreatedBy],
				[CreatedFromIP],
				[IsDeleted],
				[LastModified],
				[LastModifiedBy],
				[LastModifiedFromIP],
				[TaxClassGuid],
				[Url]
) 

VALUES 
(
				@Guid,
				@StoreGuid,
				@IsVisible,
				@IsSpecial,
				@Created,
				@CreatedBy,
				@CreatedFromIP,
				@IsDeleted,
				@LastModified,
				@LastModifiedBy,
				@LastModifiedFromIP,
				@TaxClassGuid,
				@Url
				
)


GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Offer_Update]

/*
Author:   			Joe Audette
Created: 			2007-02-28
Last Modified: 		2008-07-16
*/
	
@Guid uniqueidentifier, 
@IsVisible bit, 
@IsSpecial bit, 
@TaxClassGuid uniqueidentifier,
@Url nvarchar(255),
@LastModified datetime, 
@LastModifiedBy uniqueidentifier, 
@LastModifiedFromIP nvarchar(255)


AS
UPDATE 		[dbo].[ws_Offer] 

SET
			
			[IsVisible] = @IsVisible,
			[IsSpecial] = @IsSpecial,
			[LastModified] = @LastModified,
			[LastModifiedBy] = @LastModifiedBy,
			[LastModifiedFromIP] = @LastModifiedFromIP,
			[TaxClassGuid] = @TaxClassGuid,
			[Url] = @Url
			
WHERE
			[Guid] = @Guid

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Offer_SelectOne]

/*
Author:   			Joe Audette
Created: 			2007-02-28
Last Modified: 		2008-07-16
*/

@Guid uniqueidentifier,
@LanguageGuid uniqueidentifier

AS
SELECT
		o.[Guid],
		o.[StoreGuid],
		o.[IsVisible],
		o.[IsSpecial],
		o.[TaxClassGuid],
		o.[Url],
		o.[Created],
		o.[CreatedBy],
		o.[CreatedFromIP],
		o.[IsDeleted],
		o.[DeletedTime],
		o.[DeletedBy],
		o.[DeletedFromIP],
		o.[LastModified],
		o.[LastModifiedBy],
		o.[LastModifiedFromIP],

		'Name' = COALESCE(od.[ShortDescription],
			(SELECT TOP 1 [ShortDescription] + ' needs translation'
			FROM [dbo].[ws_OfferDescription]
			WHERE [dbo].[ws_OfferDescription].[OfferGuid] = o.Guid)
			),

		'Description' = od.[LongDescription]

		
FROM
		[dbo].[ws_Offer] o

LEFT OUTER JOIN
		[dbo].[ws_OfferDescription] od

ON		o.Guid = od.OfferGuid
AND		od.LanguageGuid = @LanguageGuid
		
WHERE
		o.[Guid] = @Guid

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_OfferHistory_Insert]

/*
Author:   			Joe Audette
Created: 			2007-02-28
Last Modified: 		2008-07-16
*/

@Guid uniqueidentifier,
@OfferGuid uniqueidentifier,
@StoreGuid uniqueidentifier,
@IsVisible bit,
@IsSpecial bit,
@Created datetime,
@CreatedBy uniqueidentifier,
@CreatedFromIP nvarchar(255),
@IsDeleted bit,
@DeletedTime datetime,
@DeletedBy uniqueidentifier,
@DeletedFromIP nvarchar(255),
@LastModified datetime,
@LastModifiedBy uniqueidentifier,
@LastModifiedFromIP nvarchar(255),
@LogTime datetime,
@TaxClassGuid uniqueidentifier,
@Url nvarchar(255)

	
AS
INSERT INTO 	[dbo].[ws_OfferHistory] 
(
				[Guid],
				[OfferGuid],
				[StoreGuid],
				[TaxClassGuid],
				[IsVisible],
				[IsSpecial],
				[Created],
				[CreatedBy],
				[CreatedFromIP],
				[IsDeleted],
				[DeletedTime],
				[DeletedBy],
				[DeletedFromIP],
				[LastModified],
				[LastModifiedBy],
				[LastModifiedFromIP],
				[LogTime],
				[Url]
) 

VALUES 
(
				@Guid,
				@OfferGuid,
				@StoreGuid,
				@TaxClassGuid,
				@IsVisible,
				@IsSpecial,
				@Created,
				@CreatedBy,
				@CreatedFromIP,
				@IsDeleted,
				@DeletedTime,
				@DeletedBy,
				@DeletedFromIP,
				@LastModified,
				@LastModifiedBy,
				@LastModifiedFromIP,
				@LogTime,
				@Url
				
)


GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[ws_Offer_SelectTop10Specials]

/*
Author:   			Joe Audette
Created: 			2007-03-05
Last Modified: 		2008-07-16
*/

@StoreGuid uniqueidentifier,
@LanguageGuid uniqueidentifier,
@CurrencyGuid uniqueidentifier

AS
SELECT TOP 10
		o.[Guid],
		o.[StoreGuid],
		o.[Url],
		'Name' = COALESCE(od.[ShortDescription],
			(SELECT TOP 1 [ShortDescription] + ' needs translation'
			FROM [dbo].[ws_OfferDescription]
			WHERE [dbo].[ws_OfferDescription].[OfferGuid] = o.Guid)
			),

		'Description' = od.[LongDescription],
			

		'Currency' = c.[Title],
		c.SymbolLeft,
		c.SymbolRight,
		op.Price

		
FROM
		[dbo].[ws_Offer] o

LEFT OUTER JOIN
		[dbo].[ws_OfferDescription] od

ON		o.Guid = od.OfferGuid
AND		od.LanguageGuid = @LanguageGuid

JOIN	
		[dbo].[ws_OfferPrice] op
ON		op.[OfferGuid] = o.[Guid]
		AND op.[CurrencyGuid] = @CurrencyGuid

JOIN	
		[dbo].[mp_Currency] c
ON		c.[Guid] = op.CurrencyGuid
		
WHERE
		o.[StoreGuid] = @StoreGuid
		AND o.[IsDeleted] = 0
		AND o.[IsVisible] = 1
		AND o.[IsSpecial] = 1

ORDER BY
	COALESCE(od.[ShortDescription],
			(SELECT TOP 1 [ShortDescription] + ' needs translation'
			FROM [dbo].[ws_OfferDescription]
			WHERE [dbo].[ws_OfferDescription].[OfferGuid] = o.Guid)
			)


GO




CREATE PROCEDURE [dbo].ws_Offer_GetCount

/*
Author:   			Joe Audette
Created: 			2008-07-16
Last Modified: 		2008-07-16
*/

@StoreGuid uniqueidentifier

AS

SELECT COUNT(*) 
FROM [dbo].[ws_Offer]
WHERE [StoreGuid] = @StoreGuid
AND [IsDeleted] = 0

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_Offer_SelectPageForProductList]

-- Author:   			Joe Audette
-- Created: 			2008-07-16
-- Last Modified: 		2008-07-16

@StoreGuid uniqueidentifier,
@LanguageGuid uniqueidentifier,
@CurrencyGuid uniqueidentifier,
@PageNumber 			int,
@PageSize 			int

AS
DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
Guid UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		o.[Guid]
		
FROM
		[dbo].[ws_Offer] o

LEFT OUTER JOIN
		[dbo].[ws_OfferDescription] od

ON		o.Guid = od.OfferGuid
AND		od.LanguageGuid = @LanguageGuid
		
WHERE  o.[StoreGuid] = @StoreGuid
		AND o.[IsDeleted] = 0
		AND o.[IsVisible] = 1

ORDER BY od.[ShortDescription]

END

SELECT
		t1.*,
		'Name' = COALESCE(od.[ShortDescription],
			(SELECT TOP 1 [ShortDescription] + ' needs translation'
			FROM [dbo].[ws_OfferDescription]
			WHERE [dbo].[ws_OfferDescription].[OfferGuid] = t1.Guid)
			),

		'Description' = od.[LongDescription],
			

		'Currency' = c.[Title],
		c.SymbolLeft,
		c.SymbolRight,
		op.Price
		
FROM
		[dbo].[ws_Offer] t1

JOIN			#PageIndex t2
ON			
		t1.[Guid] = t2.[Guid]

LEFT OUTER JOIN
		[dbo].[ws_OfferDescription] od

ON		t1.Guid = od.OfferGuid
AND		od.LanguageGuid = @LanguageGuid

JOIN	
		[dbo].[ws_OfferPrice] op
ON		op.[OfferGuid] = t1.[Guid]
		AND op.[CurrencyGuid] = @CurrencyGuid

JOIN	
		[dbo].[mp_Currency] c
ON		c.[Guid] = op.CurrencyGuid
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[ws_Offer_GetCountForProductList]

/*
Author:   			Joe Audette
Created: 			2008-07-16
Last Modified: 		2008-07-16
*/

@StoreGuid uniqueidentifier

AS

SELECT COUNT(*) 
FROM [dbo].[ws_Offer]
WHERE [StoreGuid] = @StoreGuid
AND [IsDeleted] = 0
AND [IsVisible] = 1

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_OfferProduct_SelectByOffer]

/*
Author:   			Joe Audette
Created: 			2007-03-02
Last Modified: 		2008-07-16
*/

@OfferGuid uniqueidentifier,
@LanguageGuid uniqueidentifier

AS


SELECT
		op.[Guid],
		op.[OfferGuid],
		op.[ProductGuid],
		op.[FullfillType],
		op.[FullFillTermsGuid],
		op.[Quantity],
		op.[SortOrder],
		
		'Name' = COALESCE(pd.[Name],
			(SELECT TOP 1 [Name] + ' needs translation'
			FROM [dbo].[ws_ProductDescription]
			WHERE [dbo].[ws_ProductDescription].[ProductGuid] = op.ProductGuid)
			),
		'Description' = COALESCE(pd.[Description],'')
			
		
FROM
		[dbo].[ws_OfferProduct] op

LEFT OUTER JOIN
		[dbo].[ws_ProductDescription] pd

ON		op.ProductGuid = pd.ProductGuid
AND		pd.LanguageGuid = @LanguageGuid
		
WHERE
		op.[OfferGuid] = @OfferGuid
		AND op.[IsDeleted] = 0

ORDER BY
		op.[SortOrder]

GO





