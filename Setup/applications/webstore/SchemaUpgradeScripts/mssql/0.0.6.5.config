SET QUOTED_IDENTIFIER ON
GO






ALTER PROCEDURE [dbo].[ws_Offer_GetListForPageOfProducts]

-- Author:   			Joe Audette
-- Created: 			2008-10-20
-- Last Modified: 		2010-04-10

@StoreGuid		uniqueidentifier,
@PageNumber 	int,
@PageSize 		int

AS
DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
Guid UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		p.[Guid]
		
FROM
		[dbo].[ws_Product] p
		
WHERE
		p.StoreGuid = @StoreGuid
		AND p.[IsDeleted] = 0
		AND p.[ShowInProductList] = 1
		AND p.[Guid] IN (
				SELECT op.ProductGuid 
				FROM [dbo].ws_OfferProduct op
				JOIN [dbo].ws_Offer o
				ON	op.OfferGuid = o.[Guid]
				WHERE op.IsDeleted = 0
				AND o.IsDeleted = 0
				AND o.IsVisible = 1
				)

ORDER BY
		p.SortRank1, p.SortRank2, p.[Name]

END

SELECT 
o.*,
op.ProductGuid

FROM	ws_Offer o

JOIN ws_OfferProduct op
ON o.[Guid] = op.OfferGuid

WHERE 
o.StoreGuid = @StoreGuid
AND o.IsVisible = 1
AND o.IsDeleted = 0
AND op.IsDeleted = 0
AND op.ProductGuid IN
(SELECT
		t1.[Guid]
		
FROM
		[dbo].[ws_Product] t1

JOIN			#PageIndex t2
ON			
		t1.[Guid] = t2.[Guid]

WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
)

ORDER BY o.SortRank1, o.SortRank2, o.Price

DROP TABLE #PageIndex




GO

