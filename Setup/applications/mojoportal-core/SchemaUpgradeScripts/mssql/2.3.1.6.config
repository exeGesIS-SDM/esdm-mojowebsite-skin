SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER ON
GO





ALTER PROCEDURE [dbo].[mp_Modules_SelectPage]

/*
Author:			Joe Audette
Created:		2006-05-15
Last Modified:	2009-0827

*/

@SiteID			int,
@ModuleDefID	int,
@Title nvarchar(255),
@PageNumber 	int,
@PageSize 		int,
@SortByModuleType	bit,
@SortByAuthor	bit



AS
DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
	ModuleID int
)	


 IF @SortByModuleType = 1
	BEGIN
	    	INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			[dbo].mp_Modules m
			JOIN			[dbo].mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			WHERE 			m.SiteID = @SiteID
							AND ((m.ModuleDefID = @ModuleDefID) OR (@ModuleDefID = -1))
							AND ((m.ModuleTitle LIKE '%' + @Title + '%') OR (@Title = ''))
							AND md.IsAdmin = 0
				
			ORDER BY 		md.FeatureName, m.ModuleTitle

	END
 Else IF @SortByAuthor = 1
	BEGIN
		INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			[dbo].mp_Modules m
			JOIN			[dbo].mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			LEFT OUTER JOIN	mp_Users u
			ON				m.CreatedByUserID = u.UserID
			WHERE 			m.SiteID = @SiteID
							AND ((m.ModuleDefID = @ModuleDefID) OR (@ModuleDefID = -1))
							AND ((m.ModuleTitle LIKE '%' + @Title + '%') OR (@Title = ''))
							AND md.IsAdmin = 0
				
			ORDER BY 		u.[Name], m.ModuleTitle
	END
 ELSE
	BEGIN
	    	INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			[dbo].mp_Modules m
			JOIN			[dbo].mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			WHERE 			m.SiteID = @SiteID
							AND ((m.ModuleDefID = @ModuleDefID) OR (@ModuleDefID = -1))
							AND ((m.ModuleTitle LIKE '%' + @Title + '%') OR (@Title = ''))
							AND md.IsAdmin = 0
				
			ORDER BY 		m.ModuleTitle

	END




SELECT		m.*,
			md.FeatureName,
			md.ControlSrc,
			md.ResourceFile,
			u.[Name] As CreatedBy,
			(SELECT COUNT(pm.PageID) FROM [dbo].mp_PageModules pm WHERE pm.ModuleID = m.ModuleID) AS UseCount

FROM			mp_Modules m

JOIN			mp_ModuleDefinitions md
ON				md.ModuleDefID = m.ModuleDefID

LEFT OUTER JOIN	mp_Users u
ON				m.CreatedByUserID = u.UserID

JOIN			#PageIndex p
ON				m.ModuleID = p.ModuleID

WHERE 		
			p.IndexID > @PageLowerBound 
			AND p.IndexID < @PageUpperBound

ORDER BY		p.IndexID

DROP TABLE #PageIndex



GO

