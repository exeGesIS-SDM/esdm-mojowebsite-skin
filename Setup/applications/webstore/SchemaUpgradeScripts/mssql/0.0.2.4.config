ALTER TABLE [dbo].ws_Offer ADD
	ShowDetailLink bit NULL 
GO

UPDATE [dbo].ws_Offer SET ShowDetailLink = 0

GO

ALTER TABLE [dbo].ws_Offer ADD
	[ProductListName] nvarchar(255) NULL 
GO


ALTER TABLE [dbo].ws_Cart DROP COLUMN CurrencyGuid
ALTER TABLE [dbo].ws_Cart DROP CONSTRAINT DF_ws_Cart_CurrencyValue
ALTER TABLE [dbo].ws_Cart DROP COLUMN CurrencyValue


GO

CREATE NONCLUSTERED INDEX [idxCartSGuid] ON [dbo].[ws_Cart] 
(
	[StoreGuid] ASC
) ON [PRIMARY]
GO


CREATE NONCLUSTERED INDEX [idxCartUGuid] ON [dbo].[ws_Cart] 
(
	[UserGuid] ASC
) ON [PRIMARY]
GO

CREATE NONCLUSTERED INDEX [idxCartIP] ON [dbo].[ws_Cart] 
(
	[CreatedFromIP] ASC
) ON [PRIMARY]
GO

CREATE NONCLUSTERED INDEX [idxOrderUGuid] ON [dbo].[ws_Order] 
(
	[UserGuid] ASC
) ON [PRIMARY]
GO




SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Cart_Insert]

/*
Author:   			Joe Audette
Created: 			2007-03-05
Last Modified: 		2008-10-18
*/

@CartGuid uniqueidentifier,
@StoreGuid uniqueidentifier,
@UserGuid uniqueidentifier,
@SubTotal decimal(15, 4),
@ShippingTotal decimal(15, 4),
@TaxTotal decimal(15, 4),
@OrderTotal decimal(15, 4),
@Created datetime,
@CreatedFromIP nvarchar(255),
@LastModified datetime,
@LastUserActivity datetime

	
AS
INSERT INTO 	[dbo].[ws_Cart] 
(
				[CartGuid],
				[StoreGuid],
				[UserGuid],
				[SubTotal],
				[ShippingTotal],
				[TaxTotal],
				[OrderTotal],
				[Created],
				[CreatedFromIP],
				[LastModified],
				[LastUserActivity]
) 

VALUES 
(
				@CartGuid,
				@StoreGuid,
				@UserGuid,
				@SubTotal,
				@ShippingTotal,
				@TaxTotal,
				@OrderTotal,
				@Created,
				@CreatedFromIP,
				@LastModified,
				@LastUserActivity
				
)


GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Cart_SelectOne]

/*
Author:   			Joe Audette
Created: 			2007-03-05
Last Modified: 		2008-10-18
*/

@CartGuid uniqueidentifier

AS
SELECT *
		
FROM
		[dbo].[ws_Cart]
		
WHERE
		[CartGuid] = @CartGuid

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Cart_SelectOneByUser]

/*
Author:   			Joe Audette
Created: 			2007-03-05
Last Modified: 		2008-10-18
*/

@UserGuid uniqueidentifier

AS
SELECT	TOP 1 *
		
FROM
		[dbo].[ws_Cart]
		
WHERE
		[UserGuid] = @UserGuid

ORDER BY [LastModified] DESC

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Cart_Update]

/*
Author:   			Joe Audette
Created: 			2007-03-05
Last Modified: 		2008-10-18
*/
	
@CartGuid uniqueidentifier, 
@UserGuid uniqueidentifier, 
@SubTotal decimal(15, 4), 
@ShippingTotal decimal(15, 4), 
@TaxTotal decimal(15, 4), 
@OrderTotal decimal(15, 4), 
@LastModified datetime, 
@LastUserActivity datetime 


AS
UPDATE 		[dbo].[ws_Cart] 

SET
			
			[UserGuid] = @UserGuid,
			[SubTotal] = @SubTotal,
			[ShippingTotal] = @ShippingTotal,
			[TaxTotal] = @TaxTotal,
			[OrderTotal] = @OrderTotal,
			[LastModified] = @LastModified,
			[LastUserActivity] = @LastUserActivity
			
WHERE
			[CartGuid] = @CartGuid

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_Cart_DeleteAnonymousByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_Cart]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND UserGuid = '00000000-0000-0000-0000-000000000000'
		AND Created < @OlderThan
)

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_Cart_DeleteByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_Cart]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND Created < @OlderThan
)

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_CartOffers_DeleteAnonymousByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_CartOffers]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND UserGuid = '00000000-0000-0000-0000-000000000000'
		AND Created < @OlderThan
)

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_CartOrderInfo_DeleteAnonymousByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_CartOrderInfo]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND UserGuid = '00000000-0000-0000-0000-000000000000'
		AND Created < @OlderThan
)

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_CartOrderInfo_DeleteByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_CartOrderInfo]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND Created < @OlderThan
)

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_CartOffers_DeleteByStore]

/*
Author:   			Joe Audette
Created: 			2008-10-18
Last Modified: 		2008-10-18
*/

@StoreGuid uniqueidentifier,
@OlderThan DateTime

AS

DELETE FROM [dbo].[ws_CartOffers]
WHERE
	[CartGuid] IN
(
SELECT CartGuid 
FROM [dbo].[ws_Cart]
WHERE	StoreGuid = @StoreGuid
		AND Created < @OlderThan
)


GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[ws_Offer_Insert]

/*
Author:   			Joe Audette
Created: 			2007-02-27
Last Modified: 		2008-10-20
*/

@Guid uniqueidentifier,
@StoreGuid uniqueidentifier,
@IsVisible bit,
@IsSpecial bit,
@TaxClassGuid uniqueidentifier,
@Url nvarchar(255),
@Created datetime,
@CreatedBy uniqueidentifier,
@CreatedFromIP nvarchar(255),
@LastModified datetime,
@LastModifiedBy uniqueidentifier,
@LastModifiedFromIP nvarchar(255),
@IsDonation bit,
@Name nvarchar(255),
@Description ntext,
@Abstract ntext,
@Price decimal(15,4),
@ProductListName nvarchar(255),
@ShowDetailLink bit

	
AS
INSERT INTO 	[dbo].[ws_Offer] 
(
				[Guid],
				[StoreGuid],
				[IsVisible],
				[IsSpecial],
				[Created],
				[CreatedBy],
				[CreatedFromIP],
				[LastModified],
				[LastModifiedBy],
				[LastModifiedFromIP],
				[TaxClassGuid],
				[Url],
				[IsDonation],
				[Name],
				[Description],
				[Abstract],
				[Price],
				[ProductListName],
				[ShowDetailLink]
) 

VALUES 
(
				@Guid,
				@StoreGuid,
				@IsVisible,
				@IsSpecial,
				@Created,
				@CreatedBy,
				@CreatedFromIP,
				@LastModified,
				@LastModifiedBy,
				@LastModifiedFromIP,
				@TaxClassGuid,
				@Url,
				@IsDonation,
				@Name,
				@Description,
				@Abstract,
				@Price,
				@ProductListName,
				@ShowDetailLink
				
)


GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[ws_Offer_Update]

/*
Author:   			Joe Audette
Created: 			2007-02-28
Last Modified: 		2008-10-20
*/
	
@Guid uniqueidentifier, 
@IsVisible bit, 
@IsSpecial bit, 
@TaxClassGuid uniqueidentifier,
@Url nvarchar(255),
@LastModified datetime, 
@LastModifiedBy uniqueidentifier, 
@LastModifiedFromIP nvarchar(255),
@IsDonation bit,
@Name nvarchar(255),
@Description ntext,
@Abstract ntext,
@Price decimal(15,4),
@ProductListName nvarchar(255),
@ShowDetailLink bit


AS
UPDATE 		[dbo].[ws_Offer] 

SET
			
			[IsVisible] = @IsVisible,
			[IsSpecial] = @IsSpecial,
			[LastModified] = @LastModified,
			[LastModifiedBy] = @LastModifiedBy,
			[LastModifiedFromIP] = @LastModifiedFromIP,
			[TaxClassGuid] = @TaxClassGuid,
			[Url] = @Url,
			[IsDonation] = @IsDonation,
			[Name] = @Name,
			[Description] = @Description,
			[Abstract] = @Abstract,
			[Price] = @Price,
			[ProductListName] = @ProductListName,
			[ShowDetailLink] = @ShowDetailLink
			
WHERE
			[Guid] = @Guid


GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[ws_Offer_GetListForPageOfProducts]

-- Author:   			Joe Audette
-- Created: 			2008-10-20
-- Last Modified: 		2008-10-20

@StoreGuid		uniqueidentifier,
@PageNumber 	int,
@PageSize 		int

AS
DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
Guid UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		p.[Guid]
		
FROM
		[dbo].[ws_Product] p
		
WHERE
		p.StoreGuid = @StoreGuid
		AND p.[IsDeleted] = 0

ORDER BY
		p.[Name]

END

SELECT 
o.*,
op.ProductGuid

FROM	ws_Offer o

JOIN ws_OfferProduct op
ON o.Guid = op.OfferGuid

WHERE 
o.StoreGuid = @StoreGuid
AND o.IsVisible = 1
AND o.IsDeleted = 0
AND op.IsDeleted = 0
AND op.ProductGuid IN
(SELECT
		t1.Guid
		
FROM
		[dbo].[ws_Product] t1

JOIN			#PageIndex t2
ON			
		t1.[Guid] = t2.[Guid]

WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
)

ORDER BY o.Price

DROP TABLE #PageIndex

GO








