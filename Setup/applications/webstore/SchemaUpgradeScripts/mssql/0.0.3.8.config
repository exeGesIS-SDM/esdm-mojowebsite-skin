SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[ws_Order_EnsureReportData]
/*
Author:   			Joe Audette
Created: 			2009-01-31
Last Modified: 		2009-01-31
*/
@ModuleGuid uniqueidentifier,
@PageID int

AS

INSERT INTO [dbo].[mp_CommerceReport]
(RowGuid,SiteGuid,UserGuid,FeatureGuid,ModuleGuid,ModuleTitle,OrderGuid,ItemGuid,ItemName,
Quantity,Price,SubTotal,OrderDateUtc,PaymentMethod,IPAddress,AdminOrderLink,
UserOrderLink,RowCreatedUtc)

SELECT
oo.ItemGuid AS RowGuid,
s.SiteGuid,
o.UserGuid,
m.FeatureGuid,
m.[Guid] AS ModuleGuid,
m.ModuleTitle,
o.OrderGuid,
oo.OfferGuid AS ItemGuid,
p.[Name] AS ItemName,
oo.Quantity,
oo.OfferPrice AS Price,
(oo.Quantity * oo.OfferPrice) AS SubTotal,
o.Created AS OrderDateUtc,
o.PaymentMethod,
o.CreatedFromIP AS IPAddress,
'AdminOrderLink' = '/WebStore/AdminOrderDetail.aspx?pageid=' 
				+ Convert(varchar(20), @PageID)
				+ '&amp;mid=' + Convert(varchar(20), m.ModuleID)
				+ '&amp;order=' + CONVERT(varchar(36), o.OrderGuid),
'UserOrderLink' = '/WebStore/OrderDetail.aspx?pageid=' 
				+ Convert(varchar(20), @PageID)
				+ '&amp;mid=' + Convert(varchar(20), m.ModuleID)
				+ '&amp;order=' + CONVERT(varchar(36), o.OrderGuid),
GetUtcDate() As RowCreatedUtc

FROM
[dbo].[ws_Store] s

JOIN
[dbo].[mp_Modules] m
ON s.ModuleID = m.ModuleID

JOIN
[dbo].[ws_Order] o
ON s.Guid = o.StoreGuid

JOIN
[dbo].[ws_OrderOffers] oo
ON oo.OrderGuid = o.OrderGuid

JOIN
[dbo].[ws_Offer] p
ON oo.OfferGuid = p.[Guid]

LEFT OUTER JOIN
[dbo].mp_CommerceReport cr
ON
cr.RowGuid = oo.ItemGuid

WHERE
cr.RowGuid IS NULL
AND m.[Guid] = @ModuleGuid


GO

